import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'

def versionObj = new Version(major: 0, minor: 2, revision: 1)
group = "net.dv8tion"
archivesBaseName = "JDA-Player"
version = "$versionObj"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.eclipse.dependsOn(cleanEclipse)

def filteredSourceDir = file("$buildDir/filtered")

// This is to fix eclipse's stupidity when first setting up the project.
// It thinks that "filtered" should be a development source folder and cries when it doesn't exist (gradlew clean)
eclipse {
    classpath {
        file {
            withXml {
                def sb = it.asString()
                sb.replace(0, sb.length(), sb.toString().replace("\n\t<classpathentry kind=\"src\" path=\"build/filtered\"/>", ""));
            }
        }
    }
}


sourceSets {
    // This source set will contain all sources that we filter
    filtered {
        java {
            srcDirs = [
                    filteredSourceDir,
                    "src/test/java",
            ]
        }
    }
}

javadoc {
    failOnError = false
}

// copy the main sources and filter any '@buildVersion@' occurences.
task processVersion (type: Copy) {
    from sourceSets.main.java
    into filteredSourceDir
    filter(ReplaceTokens, tokens: [
            versionMajor: versionObj.major.toString(),
            versionMinor: versionObj.minor.toString(),
            versionRevision: versionObj.revision.toString(),
            versionBuild: versionObj.build.toString()
    ])
}

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version
    }
    include "net/dv8tion/jda/**"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.filtered.java
    include "net/dv8tion/jda/**"
}

// create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version
    }

    baseName = project.name + '-withDependencies'
    from {
        configurations.compile.collect {
            dependency ->
                if (dependency.directory) { // If it is a folder, just include the folder in its entirety
                    return dependency
                } else {    // If it isn't a folder, put it in a zipTree. if it is a zip or jar the contents will be extracted.
                    return zipTree(dependency)
                }
        }
    }
    with jar
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}
repositories {
    jcenter()
}

dependencies {
    compile 'net.dv8tion:JDA:2.0.0_242'
}

class Version {
    int major, minor, revision

    String getBuild() {
        System.getenv("BUILD_NUMBER") ?: System.getProperty("BUILD_NUMBER") ?: "DEV"
    }

    String toString() {
        "${major}.${minor}.${revision}_$build"
    }
}

uploadArchives {
    onlyIf {
        System.getenv("BUILD_NUMBER")
    }
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            pom.project {
                name 'JDA-Player'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'An AudioPlayer for JDA that provides functionality to play local files and stream from remote sources.'
                url 'https://github.com/DV8FromTheWorld/JDA-Player'

                scm {
                    connection 'scm:git:git@github.com:DV8FromTheWorld/JDA-Player.git'
                    developerConnection 'scm:git:git@github.com:DV8FromTheWorld/JDA-Player.git'
                    url 'scm:git:git@github.com:DV8FromTheWorld/JDA-Player.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'DV8FromTheWorld'
                        name 'Austin Keener'
                        email 'keeneraustin@yahoo.com'
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    pkg {
        repo = 'maven'
        name = 'JDA-Player'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/DV8FromTheWorld/JDA-Player.git'
        configurations = ['archives']
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

bintrayUpload {
    onlyIf {
        System.getenv("BUILD_NUMBER")
    }
}

// tell the compileJava task to compile the filtered source
compileJava.source = sourceSets.filtered.java
compileJava.dependsOn processVersion

// Creates the w/ dependencies jar.
uploadArchives.dependsOn fatJar
bintrayUpload.dependsOn uploadArchives

// If we don't have the username and password for uploading, we probably also
// can't sign the archives, so skip these tasks.
signArchives.onlyIf { !ossrhUsername.empty && !ossrhPassword.empty }
uploadArchives.onlyIf { !ossrhUsername.empty && !ossrhPassword.empty }
bintrayUpload.onlyIf { !bintrayUsername.empty && !bintrayApiKey.empty }
